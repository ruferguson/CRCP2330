class Fruit {
	field int xP, yP; // location of the fruit's top-left corner
  field int x, y; // x and y size of this fruit

  /** Constructs a new Fruit with a given location and size. */
  constructor Fruit new() {
    let xP = Random.randRange(500);
    let yP = 0;
    let x = 3 + Random.randRange(5);
    let y = 3 + Random.randRange(5);
    return this;
  }

  /** Disposes this fruit. */
  method void dispose() {
    do Memory.deAlloc(this);
    return;
  }

  /** Draws the fruit on the screen. */
  method void draw() {
    do Screen.setColor(true);
    do Screen.drawRectangle(xP, yP, x + xP, y + yP);
    return;
  }

  method boolean intersectGrass() {
    if (yP > 235) {    
      do erase();
      return true;
    } else {
      return false;  
    }
  }

  method boolean intersectBasket(int xp1, int x1, int yp1, int y1) {
    if ((yP > yp1) & (xP > xp1) & ((x + xP) < x1)) {
      do erase();
      return true;
    } else {
      return false;
    }
  }

  /** Erases the fruit from the screen. */
  method void erase() {
    do Screen.setColor(false);
    do Screen.drawRectangle(xP, yP, x + xP, y + yP);
    return;
  }

  /** Moves the fruit down by 2 pixels. */
  method void fall() {
    if ((yP + y) < 245) {
      do Screen.setColor(false);
      do Screen.drawRectangle(xP, yP, xP + x, yP + y + 1);
      let yP = yP + 1;
      do Screen.setColor(true);
      do Screen.drawRectangle(xP, yP, xP + x, yP + y);
    }
    return;
  }

}